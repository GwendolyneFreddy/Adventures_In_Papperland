BACKUP ~aip\backup~
AUTHOR ~Charles Bisson, Badgert(WeiDU)~

VERSION ~v5~

README ~aip/readme-aip.txt~

ALWAYS

	/* --------------------------------------------------------------------------------------------- *
	 *    Convert strings to UTF-8 for EE games                                                    *
	 *    No exception, everything needs to be converted                                             *
	 *    setup.tra contains in game texts as well as installation texts and need to be converted    *
	 * -----------------------------------------------------------------------*--------------------- */
	ACTION_DEFINE_ARRAY cb#noconvert BEGIN END
	// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
	ACTION_DEFINE_ARRAY cb#reload BEGIN setup END

	LAF HANDLE_CHARSETS
		INT_VAR
			infer_charsets = 1
		STR_VAR
			tra_path = EVAL ~%MOD_FOLDER%/lang~
			noconvert_array = cb#noconvert
			reload_array = cb#reload
	END

END


AUTO_TRA ~aip/lang/%s~

LANGUAGE ~English~
         ~english~
         ~aip/lang/english/setup.tra~

LANGUAGE ~Russian (translation Austin & aerie.ru)~
         ~russian~
         ~aip/lang/russian/setup.tra~

LANGUAGE ~Deutsch (Uebersetzung von Drogan Di'Umptu)~
         ~german~
         ~aip/lang/german/setup.tra~

LANGUAGE ~Espanol (traduccion por Clan REO)~
         ~spanish~
         ~aip/lang/spanish/setup.tra~


/* ========================== *
 *  Adventures in Papperland  *
 * ========================== */
BEGIN @0
DESIGNATED 0 LABEL ~adventures_in_papperland~
REQUIRE_PREDICATE GAME_IS ~bg2 tob bgt bg2ee eet~ @1	// ~You must have BG2, ToB, BGT, BG2:EE or EET installed to install Adventures In Papperland.~

/* ------------------- *
 *  Install creatures  *
 * ------------------- */
COPY ~aip/cre/cbgeohar.cre~ ~override~
	SAY NAME1 @11
	SAY NAME2 @11

COPY ~aip/cre/cbjohnln.cre~ ~override~
	SAY NAME1 @12
	SAY NAME2 @12

COPY ~aip/cre/cbpaulmc.cre~ ~override~
	SAY NAME1 @13
	SAY NAME2 @13

COPY ~aip/cre/cbringos.cre~ ~override~
	SAY NAME1 @14
	SAY NAME2 @14

COPY ~aip/misc~         ~override~

/* ------------------------------- *
 *  Compiling scripts and dialogs  *
 * ------------------------------- */
EXTEND_BOTTOM ~ar0510.bcs~	~aip/scripts/sar0510.baf~

COMPILE ~aip/baf~

COMPILE ~aip/d~

/////////////////////////////////////
//  End of Adventures In Papperland
/////////////////////////////////////
